buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.14"
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.1"
  }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"

apply plugin: "eclipse"

// The “ratpack” plugin is IDEA aware.
// It will create a run configuration in IDEA to launch your app in your IDE, with hot reloading.
apply plugin: "idea"

idea {
    project {
        //use JDK 1.8 in idea
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

repositories {
  jcenter()
  maven { url 'http://clinker.netty.io/nexus/content/repositories/snapshots' }
}

dependencies {
  // SpringLoaded enables runtime hot reloading.
  // It is not part of the app runtime and is not shipped in the distribution.
  springloaded "org.springframework:springloaded:1.2.3.RELEASE"

  // Default SLF4J binding.  Note that this is a blocking implementation.
  // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
  runtime 'org.slf4j:slf4j-simple:1.7.10'

  testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
  testCompile 'junit:junit:4.12'
  compile ratpack.dependency("guice")
  compile ratpack.dependency("hystrix")
  compile "com.netflix.archaius:archaius-core:0.6.5"
}

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("Must be built with Java 8 or higher")
}

mainClassName = "com.epages.hackathon.HackApp"